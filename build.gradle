buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "java"
    id "signing"
    id "maven-publish"
//    id "org.hibernate.build.maven-repo-auth" version "3.0.0"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "de.codecentric:spring-boot-admin-starter-server:2.1.3"
}

group = "com.ersione"
version = "1.0.3"

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId = "com.ersione"
            artifactId = "spring-boot-admin-jandi-notifier"
            version = "1.0.3"

            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }


    repositories {
        maven {
            name "bintray"
            url "https://api.bintray.com/maven/ersione/com.ersione/spring-boot-admin-jandi-notifier/;publish=1"
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name "spring-boot-admin-jandi-notifier"
            description "Spring boot admin notifier for Jandi"
            url "https://github.com/ersione/spring-boot-admin-jandi-notifier"
            organization {
                name "com.ersione"
                url "https://github.com/ersione"
            }
            issueManagement {
                system "GitHub"
                url "https://github.com/ersione/spring-boot-admin-jandi-notifier/issues"
            }
            licenses {
                license {
                    name "Apache License 2.0"
                    url "https://github.com/ersione/spring-boot-admin-jandi-notifier/blob/master/LICENSE"
                    distribution "repo"
                }
            }
            scm {
                url "https://github.com/ersione/spring-boot-admin-jandi-notifier"
                connection "scm:git:git@github.com/ersione/spring-boot-admin-jandi-notifier.git"
                developerConnection "scm:git:git@github.com/ersione/spring-boot-admin-jandi-notifier.git"
            }
            developers {
                developer {
                    name "Ersione"
                }
            }
        }
    }
}